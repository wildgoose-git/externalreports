

&НаКлиенте
Процедура Печать(ИдентификаторКоманды, ОбъектыНазначенияМассив) Экспорт
	
	Если ИдентификаторКоманды ="ПФ_MXL_УведомлениеОРасторжении" Тогда
		ПечатьУведомления(ОбъектыНазначенияМассив)
	КонецЕсли;
	
	Если ИдентификаторКоманды = "ПФ_MXL_СправкаПриУвольнении" Тогда
		ПечатьСправки(ОбъектыНазначенияМассив);
	КонецЕсли;
	Если ИдентификаторКоманды = "ПФ_MXL_ДополнительныйЛист" Тогда
		ПечатьДополнительныйЛист(ОбъектыНазначенияМассив);
	КонецЕсли;
	Если ИдентификаторКоманды = "ПФ_MXL_УведомлениеПакет" Тогда
		ПечатьПакет(ОбъектыНазначенияМассив);
	КонецЕсли;

КонецПроцедуры


#Область ПроцедурыПечати

&НаКлиенте
Процедура ПечатьУведомления(ОбъектыНазначенияМассив)

	АдресВоВременномХранилище = ПолучитьДанныеПечати(ОбъектыНазначенияМассив);
	СтруктураДанных = ПолучитьИзВременногоХранилища(АдресВоВременномХранилище);

	//Инициализация Word
	Попытка	
		
		ОбъектWORD = Новый COMОбъект("Word.Application");
				
	Исключение
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = "Возникла проблема с созданием файла Word";
		СообщениеПользователю.Сообщить();
		
		ОбъектWORD = Неопределено;
		
		Возврат;
	КонецПопытки;
	
	
	//Заполнение шаблонов
	Для каждого КлючИЗначение Из  СтруктураДанных.СоответствиеДанныеОбъектов Цикл
		
		Попытка
			
			ИмяВременногоФайла_Word = ПолучитьИмяВременногоФайла(".docx");

			СтруктураДанных.МакетУведомление.Записать(ИмяВременногоФайла_Word); //Двоичные данные
			
			ПечатнаяФорма = ОбъектWORD.Documents.Open(ИмяВременногоФайла_Word);
			
			ПечатнаяФорма.Activate();

			//
			ЗаполнениеУведомления(КлючИЗначение.Значение,СтруктураДанных.ДанныеИнспектораПоКадрам,ПечатнаяФорма);
			
			ПечатнаяФорма.Save();	
			
			
		Исключение
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.Текст = ОписаниеОшибки();
			СообщениеПользователю.Сообщить();
			ОбъектWORD.Quit();
			ОбъектWORD = Неопределено;
			
			Возврат;
			
		КонецПопытки;
		
	КонецЦикла; 
	
	ОбъектWORD.Visible  = Истина;
	ОбъектWORD 			= Неопределено;


КонецПроцедуры // печатьУведомления()

&НаКлиенте
Процедура ЗаполнениеУведомления(ДанныеОбъекта,ДанныеИнспектора,ПечатнаяФорма)

	СтруктураТаблиц =  СтруктураТаблицПоЗаголовкам(ПечатнаяФорма);
	
	//Контактный телефон Инспектовра
	ТаблицаВорд 	 = СтруктураТаблиц.Таблица_КонтактныйТелефонИсполнителя;
	ТекстВвода 		 = ДанныеИнспектора.НомерТелефона;
	НачальнаяКолонка = 2;
	Если ТаблицаВорд.Columns.Count - НачальнаяКолонка + 1 < СтрДлина(ТекстВвода) Тогда 
		ВызватьИсключение (НСтр("ru = 'Длина таблицы ""Контактный телефон"" меньше текста ввода'"));
	КонецЕсли;
	ЗаполнитьТаблицуТекстом(ТаблицаВорд,ТекстВвода,НачальнаяКолонка);

	//Фамилия 
	ТаблицаВорд 	 = СтруктураТаблиц.Таблица_Фамилия;
	ТекстВвода 		 = ДанныеОбъекта.Фамилия;
	НачальнаяКолонка = 2;
	Если ТаблицаВорд.Columns.Count - НачальнаяКолонка + 1 < СтрДлина(ТекстВвода) Тогда 
		ВызватьИсключение (НСтр("ru = 'Длина таблицы ""Фамилия"" меньше текста ввода'"));
	КонецЕсли;
	ЗаполнитьТаблицуТекстом(ТаблицаВорд,ТекстВвода,НачальнаяКолонка);

	
	//Имя 
	ТаблицаВорд 	 = СтруктураТаблиц.Таблица_Имя;
	ТекстВвода 		 = ДанныеОбъекта.Имя;
	НачальнаяКолонка = 2;
	Если ТаблицаВорд.Columns.Count - НачальнаяКолонка + 1 < СтрДлина(ТекстВвода) Тогда 
		ВызватьИсключение (НСтр("ru = 'Длина таблицы ""Имя"" меньше текста ввода'"));
	КонецЕсли;
	ЗаполнитьТаблицуТекстом(ТаблицаВорд,ТекстВвода,НачальнаяКолонка);

	//Отчество 
	ТаблицаВорд 	 = СтруктураТаблиц.Таблица_Отчество;
	ТекстВвода 		 = ДанныеОбъекта.Отчество;
	НачальнаяКолонка = 2;
	Если ТаблицаВорд.Columns.Count - НачальнаяКолонка + 1 < СтрДлина(ТекстВвода) Тогда 
		ВызватьИсключение (НСтр("ru = 'Длина таблицы ""Отчество"" меньше текста ввода'"));
	КонецЕсли;
	ЗаполнитьТаблицуТекстом(ТаблицаВорд,ТекстВвода,НачальнаяКолонка,2);

	//Гражданство 
	ТаблицаВорд 	 = СтруктураТаблиц.Таблица_Гражданство;
	ТекстВвода 		 = ДанныеОбъекта.Гражданство;
	НачальнаяКолонка = 2;
	Если ТаблицаВорд.Columns.Count - НачальнаяКолонка + 1 < СтрДлина(ТекстВвода) Тогда 
		ВызватьИсключение (НСтр("ru = 'Длина таблицы ""Гражданство"" меньше текста ввода'"));
	КонецЕсли;
	ЗаполнитьТаблицуТекстом(ТаблицаВорд,ТекстВвода,НачальнаяКолонка);
	
	//Место рождения 
	ТаблицаВорд_1 	 = СтруктураТаблиц.Таблица_МестоРождения_1;
	ТаблицаВорд_2 	 = СтруктураТаблиц.Таблица_МестоРождения_2;
	ТекстВвода 		 = СокрЛП(СтрЗаменить(Сред(ДанныеОбъекта.МестоРождения,3),","," "));

	НачальнаяКолонка = 2;
	Если ТаблицаВорд_1.Columns.Count +ТаблицаВорд_2.Columns.Count - НачальнаяКолонка + 1 < СтрДлина(ТекстВвода) Тогда 
		ВызватьИсключение (НСтр("ru = 'Длина таблицы ""Место рождения"" меньше текста ввода'"));
	КонецЕсли;
	
	ЧислоСимволов = ТаблицаВорд_1.Columns.Count - НачальнаяКолонка + 1;
	ЗаполнитьТаблицуТекстом(ТаблицаВорд_1,Лев(ТекстВвода,ЧислоСимволов),НачальнаяКолонка,1);
	ЗаполнитьТаблицуТекстом(ТаблицаВорд_2,Сред(ТекстВвода,ЧислоСимволов+1),1);

	
	//Дата рождения 
	ТаблицаВорд 	 = СтруктураТаблиц.Таблица_ДатаРождения;
	ТекстВвода 		 = ПреобразоватьДатуВСтрокуВвода(ДанныеОбъекта.ДатаРождения);
	НачальнаяКолонка = 2;
	Если ТаблицаВорд.Columns.Count - НачальнаяКолонка + 1 < СтрДлина(ТекстВвода) Тогда 
		ВызватьИсключение (НСтр("ru = 'Длина таблицы ""Дата рождения"" меньше текста ввода'"));
	КонецЕсли;
	ЗаполнитьТаблицуТекстом(ТаблицаВорд,ТекстВвода,НачальнаяКолонка);
	

	ТаблицаВорд 	 = СтруктураТаблиц.Таблица_ПаспортСерияНомерДата;
	ТекстВвода 		 = ДанныеОбъекта.Серия;
	НачальнаяКолонка = 2;
	Если 7 < СтрДлина(ТекстВвода) Тогда 
		ВызватьИсключение (НСтр("ru = 'Длина таблицы ""Серия паспорта"" меньше текста ввода'"));
	КонецЕсли;
	ЗаполнитьТаблицуТекстом(ТаблицаВорд,ТекстВвода,НачальнаяКолонка);
	
	//номер
	ТекстВвода 		 = ДанныеОбъекта.Номер;
	НачальнаяКолонка = 10;
	Если 9 < СтрДлина(ТекстВвода) Тогда 
		ВызватьИсключение (НСтр("ru = 'Длина таблицы ""Номер паспорта"" меньше текста ввода'"));
	КонецЕсли;
	ЗаполнитьТаблицуТекстом(ТаблицаВорд,ТекстВвода,НачальнаяКолонка);


	//Дата выдачи
	ТекстВвода 		 = ПреобразоватьДатуВСтрокуВвода(ДанныеОбъекта.ДатаВыдачи);
	НачальнаяКолонка = 21;
	Если 10 < СтрДлина(ТекстВвода) Тогда 
		ВызватьИсключение (НСтр("ru = 'Длина таблицы ""Дата выдачи"" меньше текста ввода'"));
	КонецЕсли;
	ЗаполнитьТаблицуТекстом(ТаблицаВорд,ТекстВвода,НачальнаяКолонка);

	//кем выдан 
	ТаблицаВорд_1 	 = СтруктураТаблиц.Таблица_ПаспортКемВыдан_1;
	ТаблицаВорд_2 	 = СтруктураТаблиц.Таблица_ПаспортКемВыдан_2;
	ТаблицаВорд_3 	 = СтруктураТаблиц.Таблица_ПаспортКемВыдан_3;
	ТекстВвода 		 = СокрЛП(ДанныеОбъекта.КемВыдан);
	НачальнаяКолонка = 2;
	Если ТаблицаВорд_1.Columns.Count + ТаблицаВорд_2.Columns.Count + ТаблицаВорд_3.Columns.Count
		- НачальнаяКолонка + 1 < СтрДлина(ТекстВвода) Тогда 
		ВызватьИсключение (СтрШаблон(НСтр("ru = 'Длина таблицы ""Кем выдан"" меньше ""%1""'"),ТекстВвода));
	КонецЕсли;
	
	ЧислоСимволов = ТаблицаВорд_1.Columns.Count - НачальнаяКолонка + 1;
	ЗаполнитьТаблицуТекстом(ТаблицаВорд_1,Лев(ТекстВвода,ЧислоСимволов),НачальнаяКолонка);
	ЗаполнитьТаблицуТекстом(ТаблицаВорд_2,Сред(ТекстВвода,ЧислоСимволов+ 1,ТаблицаВорд_2.Columns.Count),1);
	ЗаполнитьТаблицуТекстом(ТаблицаВорд_3,Сред(ТекстВвода,ЧислоСимволов+ 1 +ТаблицаВорд_2.Columns.Count),1);

	//Патент серия 
	ТаблицаВорд 	 = СтруктураТаблиц.Таблица_Патент;
	ТекстВвода 		 = ДанныеОбъекта.ПатентСерия;
	НачальнаяКолонка = 2;
	Если 9 < СтрДлина(ТекстВвода) Тогда 
		ВызватьИсключение (НСтр("ru = 'Длина таблицы ""Серия патента"" меньше текста ввода'"));
	КонецЕсли;
	ЗаполнитьТаблицуТекстом(ТаблицаВорд,ТекстВвода,НачальнаяКолонка,2);
	
	//Патент номер  
	ТекстВвода 		 = ДанныеОбъекта.ПатентНомер;
	НачальнаяКолонка = 10;
	Если 10 < СтрДлина(ТекстВвода) Тогда 
		ВызватьИсключение (НСтр("ru = 'Длина таблицы ""Номер патента"" меньше текста ввода'"));
	КонецЕсли;
	ЗаполнитьТаблицуТекстом(ТаблицаВорд,ТекстВвода,НачальнаяКолонка,2);
	
	//Патент дата выдачи 
	ТекстВвода 		 = ПреобразоватьДатуВСтрокуВвода(ДанныеОбъекта.ПатентДатаВыдачи);
	НачальнаяКолонка = 22;
	Если ТаблицаВорд.Columns.Count - НачальнаяКолонка + 1 < СтрДлина(ТекстВвода) Тогда 
		ВызватьИсключение (НСтр("ru = 'Длина таблицы ""Дата патента"" меньше текста ввода'"));
	КонецЕсли;
	ЗаполнитьТаблицуТекстом(ТаблицаВорд,ТекстВвода,НачальнаяКолонка,2);

		
	//Патент срок действия С
	ТаблицаВорд 	 = СтруктураТаблиц.Таблица_ПатентСрокДействия;
	ТекстВвода 		 = ПреобразоватьДатуВСтрокуВвода(ДанныеОбъекта.ПатентСрокС);
	НачальнаяКолонка = 2;
	Если 10 < СтрДлина(ТекстВвода) Тогда 
		ВызватьИсключение (НСтр("ru = 'Длина таблицы ""Срок действия 1"" меньше текста ввода'"));
	КонецЕсли;
	ЗаполнитьТаблицуТекстом(ТаблицаВорд,ТекстВвода,НачальнаяКолонка);

	//Патент срок действия ДО
	ТекстВвода 		 = ПреобразоватьДатуВСтрокуВвода(ДанныеОбъекта.ПатентСрокПо);
	НачальнаяКолонка = 13;
	Если 10 < СтрДлина(ТекстВвода) Тогда 
		ВызватьИсключение (НСтр("ru = 'Длина таблицы ""Срок действия 2"" меньше текста ввода'"));
	КонецЕсли;
	ЗаполнитьТаблицуТекстом(ТаблицаВорд,ТекстВвода,НачальнаяКолонка);

	//Профессия 
	ТаблицаВорд_1 	 = СтруктураТаблиц.Таблица_Профессия_1;
	ТаблицаВорд_2 	 = СтруктураТаблиц.Таблица_Профессия_2;
	ТекстВвода 		 = ДанныеОбъекта.Должность;
	НачальнаяКолонка = 1;
	Если ТаблицаВорд_2.Columns.Count + ТаблицаВорд_1.Columns.Count - НачальнаяКолонка + 1 < СтрДлина(ТекстВвода) Тогда 
		ВызватьИсключение (НСтр("ru = 'Длина таблицы ""Профессия"" меньше текста ввода'"));
	КонецЕсли;
	
	ЧислоСимволов = ТаблицаВорд_1.Columns.Count - НачальнаяКолонка + 1;
	ЗаполнитьТаблицуТекстом(ТаблицаВорд_1,Лев(ТекстВвода,ЧислоСимволов),НачальнаяКолонка);
	ЗаполнитьТаблицуТекстом(ТаблицаВорд_2,Сред(ТекстВвода,ЧислоСимволов+1),1);

	//Дата увольнения
	ТаблицаВорд 	 = СтруктураТаблиц.Таблица_ДатаПрекращенияДоговора;
	ТекстВвода 		 = ПреобразоватьДатуВСтрокуВвода(ДанныеОбъекта.ДатаУвольнения);
	НачальнаяКолонка = 2;
	Если ТаблицаВорд.Columns.Count - НачальнаяКолонка + 1 < СтрДлина(ТекстВвода) Тогда 
		ВызватьИсключение (НСтр("ru = 'Длина таблицы ""Дата договора"" меньше текста ввода'"));
	КонецЕсли;
	ЗаполнитьТаблицуТекстом(ТаблицаВорд,ТекстВвода,НачальнаяКолонка,2);
	
	//Данные Инспектора
	ФИОИнспектора =  СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 %2.%3.",
				ДанныеИнспектора.Фамилия,
				Лев(ДанныеИнспектора.Имя,1),
				Лев(ДанныеИнспектора.Отчество,1));
				
				
	ТаблицаВорд 	 = СтруктураТаблиц.Таблица_КадрыДолжностьФамилия;
	
	Ячейка = ТаблицаВорд.Cell(1,1).Range;
	Ячейка.text = ДанныеИнспектора.Должность;

	Ячейка = ТаблицаВорд.Cell(1,3).Range;
	Ячейка.text = ФИОИнспектора;
	
	//Доверенность
	ТаблицаВорд 	 = СтруктураТаблиц.Таблица_КадрыПодвал_1;
	Ячейка 		= ТаблицаВорд.Cell(4,2).Range;
	Ячейка.text = ДанныеИнспектора.ДоверенностьНомер;

	Ячейка 		= ТаблицаВорд.Cell(4,4).Range;
	Ячейка.text = ДанныеИнспектора.ДоверенностьДата;

	Ячейка 		= ТаблицаВорд.Cell(4,6).Range;
	Ячейка.text = ДанныеИнспектора.ДоверенностьМесяц;
	
	Ячейка 		= ТаблицаВорд.Cell(4,8).Range;
	Ячейка.text = ДанныеИнспектора.ДоверенностьГод;
	
	//Паспорт инспектора   
	ТаблицаВорд 	 = СтруктураТаблиц.Таблица_КадрыПаспортСерияНомерДатаВыдачи;
	Ячейка 		= ТаблицаВорд.Cell(1,2).Range;
	Ячейка.text = ДанныеИнспектора.Серия;
	Ячейка 		= ТаблицаВорд.Cell(1,4).Range;
	Ячейка.text = ДанныеИнспектора.Номер;
	//дата выдачи паспорта
	Ячейка 		= ТаблицаВорд.Cell(1,6).Range;
	Ячейка.text = Формат(День(ДанныеИнспектора.ДатаВыдачи),"ЧЦ=2; ЧГ=0; ЧВН=");
	Ячейка 		= ТаблицаВорд.Cell(1,8).Range;
	Ячейка.text =  ПолучитьМесяцПрописью(ДанныеИнспектора.ДатаВыдачи);
	Ячейка 		= ТаблицаВорд.Cell(1,10).Range;
	Ячейка.text =   Формат(Год(ДанныеИнспектора.ДатаВыдачи),"ЧЦ=4; ЧГ=0; ЧВН=");

	//кем выдан паспорт
	ТаблицаВорд 	 = СтруктураТаблиц.Таблица_КадрыПаспортКемВыдан;
	Ячейка 		= ТаблицаВорд.Cell(1,2).Range;
	Ячейка.text =   ДанныеИнспектора.КемВыдан;

		
КонецПроцедуры // ЗаполнениеУведомления()

&НаКлиенте
Процедура ПечатьСправки(ОбъектыНазначенияМассив)

		
	АдресВоВременномХранилище = ПолучитьДанныеПечати(ОбъектыНазначенияМассив);
	СтруктураДанных = ПолучитьИзВременногоХранилища(АдресВоВременномХранилище);

	//Инициализация Word
	Попытка	
		ОбъектWORD = Новый COMОбъект("Word.Application");
	Исключение
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = "Возникла проблема с созданием файла Word";
		СообщениеПользователю.Сообщить();
		ОбъектWORD = Неопределено;
		Возврат;
	КонецПопытки;
	
	//Заполнение шаблонов
	Для каждого КлючИЗначение Из  СтруктураДанных.СоответствиеДанныеОбъектов Цикл
		
		Попытка
			
			ИмяВременногоФайла_Word = ПолучитьИмяВременногоФайла(".docx");

			СтруктураДанных.МакетСправка.Записать(ИмяВременногоФайла_Word); //Двоичные данные
			
			ПечатнаяФорма = ОбъектWORD.Documents.Open(ИмяВременногоФайла_Word);
			ПечатнаяФорма.Activate();

			ЗаполнитьСправку(КлючИЗначение.Значение,СтруктураДанных.ДанныеИнспектораПоКадрам,ПечатнаяФорма);
			
			ПечатнаяФорма.Save();	
			
		Исключение
			
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.Текст = ОписаниеОшибки();
			СообщениеПользователю.Сообщить();
			ОбъектWORD.Quit();
			ОбъектWORD = Неопределено;
			
			Возврат;
			
		КонецПопытки;
		
	КонецЦикла; 
	
	ОбъектWORD.Visible  = Истина;
	ОбъектWORD 			= Неопределено;


КонецПроцедуры // ПечатьСправки()

&НаКлиенте
Процедура ЗаполнитьСправку(ДанныеОбъекта,ДанныеИнспектора,ПечатнаяФорма)

	СтруктураТаблиц =  СтруктураТаблицПоЗаголовкам(ПечатнаяФорма);
	
	//Таблица_КонтактныйТелефонИсполнителя
	ТаблицаВорд 	 = СтруктураТаблиц.Таблица_Справка;
	Ячейка = ТаблицаВорд.Cell(13,1).Range;
	Ячейка.text = СтрШаблон("%1 %2 %3, %4 года рождения, увольнение %5",
	                                  ДанныеОбъекта.Фамилия,
									  ДанныеОбъекта.Имя,
									  ДанныеОбъекта.Отчество,
									Формат(ДанныеОбъекта.ДатаРождения,"ДЛФ=Д"),
									Формат(ДанныеОбъекта.ДатаУвольнения,"ДЛФ=Д"));
	
КонецПроцедуры // ЗаполнитьСправку()

&НаКлиенте
Процедура ПечатьДополнительныйЛист(ОбъектыНазначенияМассив)

		
	АдресВоВременномХранилище = ПолучитьДанныеПечати(ОбъектыНазначенияМассив);
	СтруктураДанных = ПолучитьИзВременногоХранилища(АдресВоВременномХранилище);

	//Инициализация Word
	Попытка	
		ОбъектWORD = Новый COMОбъект("Word.Application");
	Исключение
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = "Возникла проблема с созданием файла Word";
		СообщениеПользователю.Сообщить();
		ОбъектWORD = Неопределено;
		Возврат;
	КонецПопытки;
	
	//Заполнение шаблонов
	Для каждого КлючИЗначение Из  СтруктураДанных.СоответствиеДанныеОбъектов Цикл
		
		Попытка
			
			ИмяВременногоФайла_Word = ПолучитьИмяВременногоФайла(".docx");

			СтруктураДанных.МакетДополнительныйЛист.Записать(ИмяВременногоФайла_Word); //Двоичные данные
			
			ПечатнаяФорма = ОбъектWORD.Documents.Open(ИмяВременногоФайла_Word);
			ПечатнаяФорма.Activate();

			ЗаполнитьДополнительныйЛист(КлючИЗначение.Значение,СтруктураДанных.ДанныеИнспектораПоКадрам,ПечатнаяФорма);
			
			ПечатнаяФорма.Save();	
			
		Исключение
			
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.Текст = ОписаниеОшибки();
			СообщениеПользователю.Сообщить();
			ОбъектWORD.Quit();
			ОбъектWORD = Неопределено;
			
			Возврат;
			
		КонецПопытки;
		
	КонецЦикла; 
	
	ОбъектWORD.Visible  = Истина;
	ОбъектWORD 			= Неопределено;


КонецПроцедуры // ПечатьУведомления()

&НаКлиенте
Процедура ЗаполнитьДополнительныйЛист(ДанныеОбъекта,ДанныеИнспектора,ПечатнаяФорма)

	СтруктураТаблиц =  СтруктураТаблицПоЗаголовкам(ПечатнаяФорма);
	
	//Фамилия
	ТаблицаВорд 	 = СтруктураТаблиц.Таблица_Фамилия;
	ТекстВвода 		 = ДанныеОбъекта.Фамилия;
	НачальнаяКолонка = 2;
	Если ТаблицаВорд.Columns.Count - НачальнаяКолонка + 1 < СтрДлина(ТекстВвода) Тогда 
		ВызватьИсключение (НСтр("ru = 'Длина таблицы ""Фамилия"" меньше текста ввода'"));
	КонецЕсли;
	ЗаполнитьТаблицуТекстом(ТаблицаВорд,ТекстВвода,НачальнаяКолонка);
	
	//Имя
	ТаблицаВорд 	 = СтруктураТаблиц.Таблица_Имя;
	ТекстВвода 		 = ДанныеОбъекта.Имя;
	НачальнаяКолонка = 2;
	Если ТаблицаВорд.Columns.Count - НачальнаяКолонка + 1 < СтрДлина(ТекстВвода) Тогда 
		ВызватьИсключение (НСтр("ru = 'Длина таблицы ""Имя"" меньше текста ввода'"));
	КонецЕсли;
	ЗаполнитьТаблицуТекстом(ТаблицаВорд,ТекстВвода,НачальнаяКолонка);

	//Отчество
	ТаблицаВорд 	 = СтруктураТаблиц.Таблица_Отчество;
	ТекстВвода 		 = ДанныеОбъекта.Отчество;
	НачальнаяКолонка = 2;
	Если ТаблицаВорд.Columns.Count - НачальнаяКолонка + 1 < СтрДлина(ТекстВвода) Тогда 
		ВызватьИсключение (НСтр("ru = 'Длина таблицы ""Отчество"" меньше текста ввода'"));
	КонецЕсли;
	ЗаполнитьТаблицуТекстом(ТаблицаВорд,ТекстВвода,НачальнаяКолонка);

	//Данные Инспектора
	ФИОИнспектора =  СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 %2.%3.",
				ДанныеИнспектора.Фамилия,
				Лев(ДанныеИнспектора.Имя,1),
				Лев(ДанныеИнспектора.Отчество,1));
				
				
	ТаблицаВорд 	 = СтруктураТаблиц.Таблица_КадрыДолжностьФамилия;
	
	Ячейка = ТаблицаВорд.Cell(1,1).Range;
	Ячейка.text = ДанныеИнспектора.Должность;

	Ячейка = ТаблицаВорд.Cell(1,3).Range;
	Ячейка.text = ФИОИнспектора;
	
	//Доверенность
	ТаблицаВорд 	 = СтруктураТаблиц.Таблица_КадрыПодвал_1;
	Ячейка 		= ТаблицаВорд.Cell(4,2).Range;
	Ячейка.text = ДанныеИнспектора.ДоверенностьНомер;

	Ячейка 		= ТаблицаВорд.Cell(4,4).Range;
	Ячейка.text = ДанныеИнспектора.ДоверенностьДата;

	Ячейка 		= ТаблицаВорд.Cell(4,6).Range;
	Ячейка.text = ДанныеИнспектора.ДоверенностьМесяц;
	
	Ячейка 		= ТаблицаВорд.Cell(4,8).Range;
	Ячейка.text = ДанныеИнспектора.ДоверенностьГод;
	
	//Паспорт инспектора   
	ТаблицаВорд 	 = СтруктураТаблиц.Таблица_КадрыПаспортСерияНомерДатаВыдачи;
	Ячейка 		= ТаблицаВорд.Cell(1,2).Range;
	Ячейка.text = ДанныеИнспектора.Серия;
	Ячейка 		= ТаблицаВорд.Cell(1,4).Range;
	Ячейка.text = ДанныеИнспектора.Номер;
	//дата выдачи паспорта
	Ячейка 		= ТаблицаВорд.Cell(1,6).Range;
	Ячейка.text = Формат(День(ДанныеИнспектора.ДатаВыдачи),"ЧЦ=2; ЧГ=0; ЧВН=");
	Ячейка 		= ТаблицаВорд.Cell(1,8).Range;
	Ячейка.text =  ПолучитьМесяцПрописью(ДанныеИнспектора.ДатаВыдачи);
	Ячейка 		= ТаблицаВорд.Cell(1,10).Range;
	Ячейка.text =   Формат(Год(ДанныеИнспектора.ДатаВыдачи),"ЧЦ=4; ЧГ=0; ЧВН=");

	//кем выдан паспорт
	ТаблицаВорд 	 = СтруктураТаблиц.Таблица_КадрыПаспортКемВыдан;
	Ячейка 		= ТаблицаВорд.Cell(1,2).Range;
	Ячейка.text =   ДанныеИнспектора.КемВыдан;
	
КонецПроцедуры // ЗаполнитьУведомлениеОЗаключениТрудовогоДоговора()

&НаКлиенте
Процедура ПечатьПакет(ОбъектыНазначенияМассив)

		
	АдресВоВременномХранилище = ПолучитьДанныеПечати(ОбъектыНазначенияМассив);
	СтруктураДанных = ПолучитьИзВременногоХранилища(АдресВоВременномХранилище);

	//Инициализация Word
	Попытка	
		ОбъектWORD = Новый COMОбъект("Word.Application");
	Исключение
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = "Возникла проблема с созданием файла Word";
		СообщениеПользователю.Сообщить();
		ОбъектWORD = Неопределено;
		Возврат;
	КонецПопытки;
	
	//Заполнение шаблонов
	Для каждого КлючИЗначение Из  СтруктураДанных.СоответствиеДанныеОбъектов Цикл
		
		Попытка
			//Уведомление
			ИмяВременногоФайла_Word_МакетУведомления = ПолучитьИмяВременногоФайла(".docx");

			СтруктураДанных.МакетУведомление.Записать(ИмяВременногоФайла_Word_МакетУведомления); //Двоичные данные
			
			ПечатнаяФорма = ОбъектWORD.Documents.Open(ИмяВременногоФайла_Word_МакетУведомления);
			ПечатнаяФорма.Activate();

			ЗаполнениеУведомления(КлючИЗначение.Значение,СтруктураДанных.ДанныеИнспектораПоКадрам,ПечатнаяФорма);
			
			ПечатнаяФорма.Save();
			//доп. лист
			ИмяВременногоФайла_Word_ДополнительныйЛист = ПолучитьИмяВременногоФайла(".docx");
			СтруктураДанных.МакетДополнительныйЛист.Записать(ИмяВременногоФайла_Word_ДополнительныйЛист); //Двоичные данные
			
			ПечатнаяФорма = ОбъектWORD.Documents.Open(ИмяВременногоФайла_Word_ДополнительныйЛист);
			ПечатнаяФорма.Activate();

			ЗаполнитьДополнительныйЛист(КлючИЗначение.Значение,СтруктураДанных.ДанныеИнспектораПоКадрам,ПечатнаяФорма);
			
			ПечатнаяФорма.Save();	

			//Справка
			ИмяВременногоФайла_Word_Справки = ПолучитьИмяВременногоФайла(".docx");

			СтруктураДанных.МакетСправка.Записать(ИмяВременногоФайла_Word_Справки); //Двоичные данные
			
			ПечатнаяФорма = ОбъектWORD.Documents.Open(ИмяВременногоФайла_Word_Справки);
			ПечатнаяФорма.Activate();

			ЗаполнитьСправку(КлючИЗначение.Значение,СтруктураДанных.ДанныеИнспектораПоКадрам,ПечатнаяФорма);
			
			ПечатнаяФорма.Save();	


		Исключение
			
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.Текст = ОписаниеОшибки();
			СообщениеПользователю.Сообщить();
			ОбъектWORD.Quit();
			ОбъектWORD = Неопределено;
			
			Возврат;
			
		КонецПопытки;
		
	КонецЦикла; 
	
	ОбъектWORD.Visible  = Истина;
	ОбъектWORD 			= Неопределено;


КонецПроцедуры // ПечатьУведомления()

#КонецОбласти 

#Область ПолучениеДанных

&НаСервереБезКонтекста
Процедура ПроверкаНаПробелыНули(СтрокаТекста)

	МассивЗнаков = Новый Массив;
	Счетчик 		= 1;
	ДлинаСтроки 	= СтрДлина(СтрокаТекста);
	Пока Счетчик <= ДлинаСтроки Цикл
		
		МассивЗнаков.Добавить(Сред(СтрокаТекста,Счетчик,1));
		
		Счетчик 		= Счетчик + 1;
	КонецЦикла; 
	
	МассивЦифр = СтрРазделить("1,2,3,4,5,6,7,8,9",",");
	Для каждого Цифра Из МассивЦифр Цикл
	
		Если Не МассивЗнаков.Найти(Цифра) = Неопределено Тогда
		
			  Возврат
		
		КонецЕсли; 
	
	КонецЦикла; 
	
	СтрокаТекста = "";

КонецПроцедуры // ПроверкаНаПробелыНули()

&НаСервере
Функция ПолучитьДанныеПечати(МассивДокументов) 

	Если Не ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Истина)
	КонецЕсли; 
	
	РезультатЗапросаПоВсемПечатаемымДокументам = ПолучитьДанныеДокументов(МассивДокументов);
		
	ВыборкаДетальныеЗаписи = РезультатЗапросаПоВсемПечатаемымДокументам.Выбрать();
	
	
	СоответствиеДанныеОбъектов = Новый Соответствие;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		СтруктураВозврата = Новый Структура;	
		Для каждого Колонка Из РезультатЗапросаПоВсемПечатаемымДокументам.Колонки Цикл
			СтруктураВозврата.Вставить(Колонка.Имя)
		КонецЦикла;
		
		ЗаполнитьЗначенияСвойств(СтруктураВозврата,ВыборкаДетальныеЗаписи);
		
		СоответствиеДанныеОбъектов.Вставить(ВыборкаДетальныеЗаписи.ФизическоеЛицо,СтруктураВозврата);
		
	
	КонецЦикла; 
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("СоответствиеДанныеОбъектов",СоответствиеДанныеОбъектов);
	СтруктураВозврата.Вставить("ДанныеИнспектораПоКадрам",ДанныеИнспектораПоКадрам());
	СтруктураВозврата.Вставить("МакетУведомление",			ОбработкаОбъект.ПолучитьМакет("МакетУведомление"));
	СтруктураВозврата.Вставить("МакетСправка",			ОбработкаОбъект.ПолучитьМакет("МакетСправка"));
	СтруктураВозврата.Вставить("МакетДополнительныйЛист",	ОбработкаОбъект.ПолучитьМакет("МакетДополнительныйЛист"));

	АдресВХранилище = ПоместитьВоВременноеХранилище(СтруктураВозврата,УникальныйИдентификатор);	
	
	Возврат АдресВХранилище;
	
КонецФункции
				
&НаСервереБезКонтекста
Функция ПолучитьДанныеДокументов(МассивДокументов)
	
	Если  Не ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Истина)
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивДокументов",МассивДокументов);
	Запрос.УстановитьПараметр("ПустаяДата",'00010101');
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Увольнение.Ссылка КАК Ссылка,
	|	Увольнение.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Увольнение.ДатаУвольнения КАК ДатаУвольнения,
	|	Увольнение.Сотрудник КАК Сотрудник,
	|	Увольнение.Организация КАК Организация,
	|	0 КАК НомерСтроки
	|ПОМЕСТИТЬ ДанныеДокументовУвольнение
	|ИЗ
	|	Документ.Увольнение КАК Увольнение
	|ГДЕ
	|	Увольнение.Ссылка В(&МассивДокументов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УвольнениеСписком.Ссылка,
	|	УвольнениеСпискомСотрудники.ФизическоеЛицо,
	|	УвольнениеСпискомСотрудники.ДатаУвольнения,
	|	УвольнениеСпискомСотрудники.Сотрудник,
	|	УвольнениеСписком.Организация,
	|	УвольнениеСпискомСотрудники.НомерСтроки
	|ИЗ
	|	Документ.УвольнениеСписком.Сотрудники КАК УвольнениеСпискомСотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УвольнениеСписком КАК УвольнениеСписком
	|		ПО УвольнениеСпискомСотрудники.Ссылка = УвольнениеСписком.Ссылка
	|ГДЕ
	|	УвольнениеСпискомСотрудники.Ссылка В(&МассивДокументов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КадроваяИсторияСотрудниковСрезПоследних.Сотрудник КАК Сотрудник,
	|	ПРЕДСТАВЛЕНИЕ(КадроваяИсторияСотрудниковСрезПоследних.Должность) КАК Должность
	|ПОМЕСТИТЬ ВременнаяТаблицаДолжностиСотрудников
	|ИЗ
	|	РегистрСведений.КадроваяИсторияСотрудников.СрезПоследних(
	|			,
	|			Сотрудник В
	|				(ВЫБРАТЬ
	|					ДанныеДокументовУвольнение.Сотрудник
	|				ИЗ
	|					ДанныеДокументовУвольнение КАК ДанныеДокументовУвольнение)) КАК КадроваяИсторияСотрудниковСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ГражданствоФизическихЛицСрезПоследних.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ГражданствоФизическихЛицСрезПоследних.Страна КАК Страна,
	|	ГражданствоФизическихЛицСрезПоследних.ИНН КАК ИНН,
	|	СтраныМира.НаименованиеПолное КАК НаименованиеПолноеСтрана
	|ПОМЕСТИТЬ ВременнаяТаблицаГражданство
	|ИЗ
	|	РегистрСведений.ГражданствоФизическихЛиц.СрезПоследних КАК ГражданствоФизическихЛицСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтраныМира КАК СтраныМира
	|		ПО ГражданствоФизическихЛицСрезПоследних.Страна = СтраныМира.Ссылка
	|ГДЕ
	|	ГражданствоФизическихЛицСрезПоследних.ФизическоеЛицо В
	|			(ВЫБРАТЬ
	|				ДанныеДокументовУвольнение.ФизическоеЛицо
	|			ИЗ
	|				ДанныеДокументовУвольнение КАК ДанныеДокументовУвольнение)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДанныеИностранца.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДанныеИностранца.РазрешениеСерия КАК РазрешениеСерия,
	|	ДанныеИностранца.РазрешениеНомер КАК РазрешениеНомер,
	|	ДанныеИностранца.РазрешениеДатаВыдачи КАК РазрешениеДатаВыдачи,
	|	ДанныеИностранца.РазрешениеДействительноДо КАК РазрешениеДействительноДо,
	|	ДанныеИностранца.РазрешениеТерриторияДействия КАК РазрешениеТерриторияДействия,
	|	ДанныеИностранца.РазрешениеКемВыдано КАК РазрешениеКемВыдано,
	|	ДанныеИностранца.РазрешениеДатаПостановкиНаМиграционныйУчет КАК РазрешениеДатаПостановкиНаМиграционныйУчет,
	|	ДанныеИностранца.РазрешениеКтоПоставилНаУчет КАК РазрешениеКтоПоставилНаУчет,
	|	ДанныеИностранца.МиграционнаяКартаСерия КАК МиграционнаяКартаСерия,
	|	ДанныеИностранца.МиграционнаяКартаНомер КАК МиграционнаяКартаНомер,
	|	ДанныеИностранца.МиграционнаяКартаДатаВыдачи КАК МиграционнаяКартаДатаВыдачи,
	|	ДанныеИностранца.ПатентСерия КАК ПатентСерия,
	|	ДанныеИностранца.ПатентНомер КАК ПатентНомер,
	|	ДанныеИностранца.ПатентДатаВыдачи КАК ПатентДатаВыдачи,
	|	ДанныеИностранца.ПатентСрокС КАК ПатентСрокС,
	|	ДанныеИностранца.ПатентСрокПо КАК ПатентСрокПо,
	|	ДанныеИностранца.ПатентНаименование КАК ПатентНаименование,
	|	ДанныеИностранца.ПатентКемВыдан КАК ПатентКемВыдан
	|ПОМЕСТИТЬ ВременнаяТаблицаРазрешенияПатенты
	|ИЗ
	|	РегистрСведений.Монолит_ДанныеИностранца КАК ДанныеИностранца
	|ГДЕ
	|	ДанныеИностранца.ФизическоеЛицо В
	|			(ВЫБРАТЬ
	|				ДанныеДокументовУвольнение.ФизическоеЛицо
	|			ИЗ
	|				ДанныеДокументовУвольнение КАК ДанныеДокументовУвольнение)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Монолит_УведомленияИностранцев.ПриказОрганизации КАК ПриказОрганизации,
	|	Монолит_УведомленияИностранцев.ГосударственнаяОрганизация КАК ГосударственнаяОрганизация,
	|	Монолит_УведомленияИностранцев.ДатаУведомления КАК ДатаУведомления
	|ПОМЕСТИТЬ ВременнаяТаблицаУведомления
	|ИЗ
	|	РегистрСведений.Монолит_УведомленияИностранцев КАК Монолит_УведомленияИностранцев
	|ГДЕ
	|	Монолит_УведомленияИностранцев.ПриказОрганизации В(&МассивДокументов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ФизическиеЛица.Ссылка КАК Ссылка,
	|	ФизическиеЛица.МестоРождения КАК МестоРождения,
	|	ФизическиеЛица.ДатаРождения КАК ДатаРождения,
	|	ФизическиеЛица.Пол КАК Пол,
	|	ФизическиеЛица.ДатаРегистрации КАК ДатаРегистрации,
	|	ФизическиеЛица.ФИО КАК ФИО,
	|	ФизическиеЛица.Фамилия КАК Фамилия,
	|	ФизическиеЛица.Отчество КАК Отчество,
	|	ФизическиеЛица.Имя КАК Имя
	|ПОМЕСТИТЬ ВременнаяТаблицаДанныеФизическогоЛица
	|ИЗ
	|	Справочник.ФизическиеЛица КАК ФизическиеЛица
	|ГДЕ
	|	ФизическиеЛица.Ссылка В
	|			(ВЫБРАТЬ
	|				ДанныеДокументовУвольнение.ФизическоеЛицо
	|			ИЗ
	|				ДанныеДокументовУвольнение КАК ДанныеДокументовУвольнение)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументыФизическихЛицСрезПоследних.Физлицо КАК Физлицо,
	|	ПРЕДСТАВЛЕНИЕ(ДокументыФизическихЛицСрезПоследних.ВидДокумента) КАК ВидДокумента,
	|	ДокументыФизическихЛицСрезПоследних.Серия КАК Серия,
	|	ДокументыФизическихЛицСрезПоследних.Номер КАК Номер,
	|	ДокументыФизическихЛицСрезПоследних.ДатаВыдачи КАК ДатаВыдачи,
	|	ДокументыФизическихЛицСрезПоследних.СрокДействия КАК СрокДействия,
	|	ДокументыФизическихЛицСрезПоследних.КемВыдан КАК КемВыдан,
	|	ДокументыФизическихЛицСрезПоследних.КодПодразделения КАК КодПодразделения
	|ПОМЕСТИТЬ ВременнаяТаблицаДокументыФизичесткогоЛица
	|ИЗ
	|	РегистрСведений.ДокументыФизическихЛиц.СрезПоследних КАК ДокументыФизическихЛицСрезПоследних
	|ГДЕ
	|	ДокументыФизическихЛицСрезПоследних.Физлицо В
	|			(ВЫБРАТЬ
	|				ДанныеДокументовУвольнение.ФизическоеЛицо
	|			ИЗ
	|				ДанныеДокументовУвольнение КАК ДанныеДокументовУвольнение)
	|	И ДокументыФизическихЛицСрезПоследних.ЯвляетсяДокументомУдостоверяющимЛичность
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокументовУвольнение.Ссылка КАК Ссылка,
	|	ДанныеДокументовУвольнение.Организация КАК Организация,
	|	ДанныеДокументовУвольнение.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДанныеДокументовУвольнение.ДатаУвольнения КАК ДатаУвольнения,
	|	ЕСТЬNULL(ВременнаяТаблицаДанныеФизическогоЛица.Фамилия, """") КАК Фамилия,
	|	ЕСТЬNULL(ВременнаяТаблицаДанныеФизическогоЛица.Имя, """") КАК Имя,
	|	ЕСТЬNULL(ВременнаяТаблицаДанныеФизическогоЛица.Отчество, """") КАК Отчество,
	|	ВременнаяТаблицаГражданство.Страна КАК Страна,
	|	ЕСТЬNULL(ВременнаяТаблицаГражданство.НаименованиеПолноеСтрана, """") КАК Гражданство,
	|	ЕСТЬNULL(ВременнаяТаблицаГражданство.ИНН, """") КАК ИННГражданство,
	|	ЕСТЬNULL(ВременнаяТаблицаРазрешенияПатенты.РазрешениеСерия, """") КАК РазрешениеСерия,
	|	ЕСТЬNULL(ВременнаяТаблицаРазрешенияПатенты.РазрешениеНомер, """") КАК РазрешениеНомер,
	|	ВременнаяТаблицаРазрешенияПатенты.РазрешениеДатаВыдачи КАК РазрешениеДатаВыдачи,
	|	ВременнаяТаблицаРазрешенияПатенты.РазрешениеДействительноДо КАК РазрешениеДействительноДо,
	|	ВременнаяТаблицаРазрешенияПатенты.РазрешениеТерриторияДействия КАК РазрешениеТерриторияДействия,
	|	ВременнаяТаблицаРазрешенияПатенты.РазрешениеКемВыдано КАК РазрешениеКемВыдано,
	|	ВременнаяТаблицаРазрешенияПатенты.РазрешениеДатаПостановкиНаМиграционныйУчет КАК РазрешениеДатаПостановкиНаМиграционныйУчет,
	|	ВременнаяТаблицаРазрешенияПатенты.РазрешениеКтоПоставилНаУчет КАК РазрешениеКтоПоставилНаУчет,
	|	ВременнаяТаблицаРазрешенияПатенты.МиграционнаяКартаСерия КАК МиграционнаяКартаСерия,
	|	ВременнаяТаблицаРазрешенияПатенты.МиграционнаяКартаНомер КАК МиграционнаяКартаНомер,
	|	ВременнаяТаблицаРазрешенияПатенты.МиграционнаяКартаДатаВыдачи КАК МиграционнаяКартаДатаВыдачи,
	|	ЕСТЬNULL(ВременнаяТаблицаРазрешенияПатенты.ПатентСерия, """") КАК ПатентСерия,
	|	ЕСТЬNULL(ВременнаяТаблицаРазрешенияПатенты.ПатентНомер, """") КАК ПатентНомер,
	|	ЕСТЬNULL(ВременнаяТаблицаРазрешенияПатенты.ПатентДатаВыдачи, &ПустаяДата) КАК ПатентДатаВыдачи,
	|	ЕСТЬNULL(ВременнаяТаблицаРазрешенияПатенты.ПатентСрокС, &ПустаяДата) КАК ПатентСрокС,
	|	ЕСТЬNULL(ВременнаяТаблицаРазрешенияПатенты.ПатентСрокПо, &ПустаяДата) КАК ПатентСрокПо,
	|	ЕСТЬNULL(ВременнаяТаблицаРазрешенияПатенты.ПатентНаименование, """") КАК ПатентНаименование,
	|	ЕСТЬNULL(ВременнаяТаблицаРазрешенияПатенты.ПатентКемВыдан, """") КАК ПатентКемВыдан,
	|	ПРЕДСТАВЛЕНИЕ(ВременнаяТаблицаУведомления.ГосударственнаяОрганизация) КАК ОрганВласти,
	|	ВременнаяТаблицаУведомления.ДатаУведомления КАК ДатаУведомления,
	|	ВременнаяТаблицаДанныеФизическогоЛица.МестоРождения КАК МестоРождения,
	|	ВременнаяТаблицаДанныеФизическогоЛица.ДатаРождения КАК ДатаРождения,
	|	ВременнаяТаблицаДанныеФизическогоЛица.Пол = ЗНАЧЕНИЕ(Перечисление.ПолФизическогоЛица.Мужской) КАК ПолМужской,
	|	ВременнаяТаблицаДанныеФизическогоЛица.ДатаРегистрации КАК ДатаРегистрации,
	|	ВременнаяТаблицаДокументыФизичесткогоЛица.ВидДокумента КАК ВидДокумента,
	|	ВременнаяТаблицаДокументыФизичесткогоЛица.Серия КАК Серия,
	|	ВременнаяТаблицаДокументыФизичесткогоЛица.Номер КАК Номер,
	|	ВременнаяТаблицаДокументыФизичесткогоЛица.ДатаВыдачи КАК ДатаВыдачи,
	|	ВременнаяТаблицаДокументыФизичесткогоЛица.СрокДействия КАК СрокДействия,
	|	ВременнаяТаблицаДокументыФизичесткогоЛица.КемВыдан КАК КемВыдан,
	|	ВременнаяТаблицаДокументыФизичесткогоЛица.КодПодразделения КАК КодПодразделения,
	|	ВременнаяТаблицаДолжностиСотрудников.Должность КАК Должность
	|ИЗ
	|	ДанныеДокументовУвольнение КАК ДанныеДокументовУвольнение
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаГражданство КАК ВременнаяТаблицаГражданство
	|		ПО ДанныеДокументовУвольнение.ФизическоеЛицо = ВременнаяТаблицаГражданство.ФизическоеЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаРазрешенияПатенты КАК ВременнаяТаблицаРазрешенияПатенты
	|		ПО ДанныеДокументовУвольнение.ФизическоеЛицо = ВременнаяТаблицаРазрешенияПатенты.ФизическоеЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаУведомления КАК ВременнаяТаблицаУведомления
	|		ПО ДанныеДокументовУвольнение.Ссылка = ВременнаяТаблицаУведомления.ПриказОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаДанныеФизическогоЛица КАК ВременнаяТаблицаДанныеФизическогоЛица
	|		ПО ДанныеДокументовУвольнение.ФизическоеЛицо = ВременнаяТаблицаДанныеФизическогоЛица.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаДокументыФизичесткогоЛица КАК ВременнаяТаблицаДокументыФизичесткогоЛица
	|		ПО ДанныеДокументовУвольнение.ФизическоеЛицо = ВременнаяТаблицаДокументыФизичесткогоЛица.Физлицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаДолжностиСотрудников КАК ВременнаяТаблицаДолжностиСотрудников
	|		ПО ДанныеДокументовУвольнение.Сотрудник = ВременнаяТаблицаДолжностиСотрудников.Сотрудник
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	ДанныеДокументовУвольнение.НомерСтроки";

	Возврат Запрос.Выполнить();

КонецФункции 
 
&НаСервереБезКонтекста
Функция ДанныеИнспектораПоКадрам()

	  Запрос = Новый Запрос;
	  Запрос.УстановитьПараметр("Пользователь",Пользователи.ТекущийПользователь());
	  Запрос.УстановитьПараметр("ВидДокумента",Справочники.ВидыДокументовФизическихЛиц.ПаспортРФ);
	  Запрос.УстановитьПараметр("Организация",Справочники.Монолит_ЗначенияСсылокСправочников.ОрганизацияМонолитстрой.ЗначениеСсылки);
	  
	  Запрос.Текст = "ВЫБРАТЬ
	  |	Пользователи.Ссылка КАК Ссылка,
	  |	Пользователи.ФизическоеЛицо КАК ФизическоеЛицо
	  |ПОМЕСТИТЬ ВременнаяТаблицаСправочникПользователи
	  |ИЗ
	  |	Справочник.Пользователи КАК Пользователи
	  |ГДЕ
	  |	Пользователи.Ссылка = &Пользователь
	  |
	  |ИНДЕКСИРОВАТЬ ПО
	  |	ФизическоеЛицо
	  |;
	  |
	  |////////////////////////////////////////////////////////////////////////////////
	  |ВЫБРАТЬ
	  |	ФизическиеЛица.Ссылка КАК Ссылка,
	  |	ФизическиеЛица.Фамилия КАК Фамилия,
	  |	ФизическиеЛица.Имя КАК Имя,
	  |	ФизическиеЛица.Отчество КАК Отчество,
	  |	ФизическиеЛица.ФИО КАК ФИО
	  |ПОМЕСТИТЬ ВременнаяТаблицаДанныеФизЛица
	  |ИЗ
	  |	Справочник.ФизическиеЛица КАК ФизическиеЛица
	  |ГДЕ
	  |	ФизическиеЛица.Ссылка В
	  |			(ВЫБРАТЬ
	  |				ВременнаяТаблицаСправочникПользователи.ФизическоеЛицо КАК ФизическоеЛицо
	  |			ИЗ
	  |				ВременнаяТаблицаСправочникПользователи КАК ВременнаяТаблицаСправочникПользователи)
	  |
	  |ИНДЕКСИРОВАТЬ ПО
	  |	Ссылка
	  |;
	  |
	  |////////////////////////////////////////////////////////////////////////////////
	  |ВЫБРАТЬ ПЕРВЫЕ 1
	  |	ФизическиеЛицаКонтактнаяИнформация.НомерТелефона КАК НомерТелефона,
	  |	ФизическиеЛицаКонтактнаяИнформация.Ссылка КАК Ссылка
	  |ПОМЕСТИТЬ ВременнаяТаблицаКонтактныйТелефон
	  |ИЗ
	  |	Справочник.ФизическиеЛица.КонтактнаяИнформация КАК ФизическиеЛицаКонтактнаяИнформация
	  |ГДЕ
	  |	ФизическиеЛицаКонтактнаяИнформация.Ссылка В
	  |			(ВЫБРАТЬ
	  |				ВременнаяТаблицаСправочникПользователи.ФизическоеЛицо КАК ФизическоеЛицо
	  |			ИЗ
	  |				ВременнаяТаблицаСправочникПользователи КАК ВременнаяТаблицаСправочникПользователи)
	  |
	  |ИНДЕКСИРОВАТЬ ПО
	  |	Ссылка
	  |;
	  |
	  |////////////////////////////////////////////////////////////////////////////////
	  |ВЫБРАТЬ ПЕРВЫЕ 1
	  |	ДокументыФизическихЛицСрезПоследних.Физлицо КАК Физлицо,
	  |	ДокументыФизическихЛицСрезПоследних.ВидДокумента КАК ВидДокумента,
	  |	ДокументыФизическихЛицСрезПоследних.Серия КАК Серия,
	  |	ДокументыФизическихЛицСрезПоследних.Номер КАК Номер,
	  |	ДокументыФизическихЛицСрезПоследних.ДатаВыдачи КАК ДатаВыдачи,
	  |	ДокументыФизическихЛицСрезПоследних.СрокДействия КАК СрокДействия,
	  |	ДокументыФизическихЛицСрезПоследних.КемВыдан КАК КемВыдан,
	  |	ДокументыФизическихЛицСрезПоследних.КодПодразделения КАК КодПодразделения,
	  |	ДокументыФизическихЛицСрезПоследних.Представление КАК Представление
	  |ПОМЕСТИТЬ ВременнаяТаблицаДокументыФизическогоЛица
	  |ИЗ
	  |	РегистрСведений.ДокументыФизическихЛиц.СрезПоследних(
	  |			,
	  |			(ВидДокумента, Физлицо) В
	  |				(ВЫБРАТЬ
	  |					&ВидДокумента КАК ВидДокумента,
	  |					ВременнаяТаблицаСправочникПользователи.ФизическоеЛицо КАК ФизическоеЛицо
	  |				ИЗ
	  |					ВременнаяТаблицаСправочникПользователи КАК ВременнаяТаблицаСправочникПользователи)) КАК ДокументыФизическихЛицСрезПоследних
	  |
	  |ИНДЕКСИРОВАТЬ ПО
	  |	Физлицо
	  |;
	  |
	  |////////////////////////////////////////////////////////////////////////////////
	  |ВЫБРАТЬ ПЕРВЫЕ 1
	  |	КадроваяИсторияСотрудниковСрезПоследних.ФизическоеЛицо КАК ФизическоеЛицо,
	  |	КадроваяИсторияСотрудниковСрезПоследних.Должность КАК Должность
	  |ПОМЕСТИТЬ ВременнаяТаблицаКадроваяИстория
	  |ИЗ
	  |	РегистрСведений.КадроваяИсторияСотрудников.СрезПоследних(
	  |			,
	  |			(ФизическоеЛицо, ГоловнаяОрганизация) В
	  |				(ВЫБРАТЬ
	  |					ВременнаяТаблицаСправочникПользователи.ФизическоеЛицо КАК ФизическоеЛицо,
	  |					&Организация КАК Организация
	  |				ИЗ
	  |					ВременнаяТаблицаСправочникПользователи КАК ВременнаяТаблицаСправочникПользователи)) КАК КадроваяИсторияСотрудниковСрезПоследних
	  |
	  |ИНДЕКСИРОВАТЬ ПО
	  |	ФизическоеЛицо
	  |;
	  |
	  |////////////////////////////////////////////////////////////////////////////////
	  |ВЫБРАТЬ
	  |	ВременнаяТаблицаДанныеФизЛица.Фамилия КАК Фамилия,
	  |	ВременнаяТаблицаДанныеФизЛица.Имя КАК Имя,
	  |	ВременнаяТаблицаДанныеФизЛица.Отчество КАК Отчество,
	  |	ВременнаяТаблицаДанныеФизЛица.ФИО КАК ФИО,
	  |	ВременнаяТаблицаКонтактныйТелефон.НомерТелефона КАК НомерТелефона,
	  |	ПРЕДСТАВЛЕНИЕ(ВременнаяТаблицаДокументыФизическогоЛица.ВидДокумента) КАК ВидДокумента,
	  |	ВременнаяТаблицаДокументыФизическогоЛица.Серия КАК Серия,
	  |	ВременнаяТаблицаДокументыФизическогоЛица.Номер КАК Номер,
	  |	ВременнаяТаблицаДокументыФизическогоЛица.ДатаВыдачи КАК ДатаВыдачи,
	  |	ВременнаяТаблицаДокументыФизическогоЛица.СрокДействия КАК СрокДействия,
	  |	ВременнаяТаблицаДокументыФизическогоЛица.КемВыдан КАК КемВыдан,
	  |	ВременнаяТаблицаДокументыФизическогоЛица.КодПодразделения КАК КодПодразделения,
	  |	ВременнаяТаблицаДокументыФизическогоЛица.Представление КАК ПредставлениеДокумента,
	  |	ПРЕДСТАВЛЕНИЕ(ВременнаяТаблицаКадроваяИстория.Должность) КАК Должность
	  |ИЗ
	  |	ВременнаяТаблицаДанныеФизЛица КАК ВременнаяТаблицаДанныеФизЛица
	  |		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКонтактныйТелефон КАК ВременнаяТаблицаКонтактныйТелефон
	  |		ПО ВременнаяТаблицаДанныеФизЛица.Ссылка = ВременнаяТаблицаКонтактныйТелефон.Ссылка
	  |		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаДокументыФизическогоЛица КАК ВременнаяТаблицаДокументыФизическогоЛица
	  |		ПО ВременнаяТаблицаДанныеФизЛица.Ссылка = ВременнаяТаблицаДокументыФизическогоЛица.Физлицо
	  |		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКадроваяИстория КАК ВременнаяТаблицаКадроваяИстория
	  |		ПО ВременнаяТаблицаДанныеФизЛица.Ссылка = ВременнаяТаблицаКадроваяИстория.ФизическоеЛицо";
	  
	  Результат = Запрос.Выполнить();
	  Выборка =  Результат.Выбрать();

	  СтруктураВозврата = Новый Структура;	
	  Для каждого Колонка Из Результат.Колонки Цикл
		  СтруктураВозврата.Вставить(Колонка.Имя)
	  КонецЦикла;

	  Если Выборка.Следующий() Тогда 
		 		  
		  ЗаполнитьЗначенияСвойств(СтруктураВозврата,Выборка);
	  Иначе
		  Для каждого КлючИЗначение Из СтруктураВозврата Цикл
		  	    КлючИЗначение.Значение = ""
		  КонецЦикла; 
		  
	  КонецЕсли; 
	  
	  //Данных по доверенности в базе не наю откуда брать
	  СтруктураВозврата.Вставить("ДоверенностьНомер","");
	  СтруктураВозврата.Вставить("ДоверенностьДата","");
	  СтруктураВозврата.Вставить("ДоверенностьМесяц","");
	  СтруктураВозврата.Вставить("ДоверенностьГод","");
	  //Если исполнитель  Гарова Ю.И. то № 6 от 18 июня 2018
	  Если СтруктураВозврата.Фамилия = "Гарова" Тогда
		  СтруктураВозврата.ДоверенностьНомер = "6";
		  СтруктураВозврата.ДоверенностьДата = "18";
		  СтруктураВозврата.ДоверенностьМесяц = "июня";
		  СтруктураВозврата.ДоверенностьГод = "18";
	  КонецЕсли; 
	  Если СтруктураВозврата.Фамилия = "Ященко" Тогда
		  СтруктураВозврата.ДоверенностьНомер = "5";
		  СтруктураВозврата.ДоверенностьДата = "10";
		  СтруктураВозврата.ДоверенностьМесяц = "января";
		  СтруктураВозврата.ДоверенностьГод = "17";
	  КонецЕсли; 
	  
	  Возврат СтруктураВозврата;
	  

КонецФункции // ПолучитьДанныеПользователя()
	
#КонецОбласти 

#Область СлужебныеПроцедуры

&НаКлиенте
Функция ПолучитьМесяцПрописью(ЗначениеДата)
	
	Если Не ТипЗнч(ЗначениеДата) = Тип("Дата") Тогда
		Возврат ""
	КонецЕсли; 
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить(1,"января");
	Соответствие.Вставить(2,"февраля");
	Соответствие.Вставить(3,"марта");
	Соответствие.Вставить(4,"апреля");
	Соответствие.Вставить(5,"мая");
	Соответствие.Вставить(6,"июня");
	Соответствие.Вставить(7,"июля");
	Соответствие.Вставить(8,"августа");
	Соответствие.Вставить(9,"сентября");
	Соответствие.Вставить(10,"октября");
	Соответствие.Вставить(11,"ноября");
	Соответствие.Вставить(12,"декабря");

	Возврат Соответствие[Месяц(ЗначениеДата)];	

КонецФункции // ПолучитьМесяцПрописью()

&НаКлиенте
Процедура ЗаполнитьТаблицуТекстом(ТаблицаВорд,ТекстВвода,НачальнаяКолонка,НомерСтроки = 1)
	Если Не ТипЗнч(ТекстВвода) = Тип("Строка") Тогда
		Возврат 
	КонецЕсли; 

	Если ПустаяСтрока(ТекстВвода) Тогда
		Возврат
	КонецЕсли; 

	Счетчик 		= 1;
	ДлинаСтроки 	= СтрДлина(ТекстВвода);
	КолонкаТаблицы 	= НачальнаяКолонка;
	ПоследняяКолонка = ТаблицаВорд.Columns.Count;
	
	Пока Счетчик <= ДлинаСтроки Цикл
		
		Если КолонкаТаблицы > ПоследняяКолонка Тогда
			Прервать;
		КонецЕсли;

		Ячейка = ТаблицаВорд.Cell(НомерСтроки,КолонкаТаблицы).Range;
		Ячейка.text = ВРег(Сред(ТекстВвода,Счетчик,1));
		
		КолонкаТаблицы = КолонкаТаблицы + 1;
		Счетчик		   = Счетчик + 1;
			
	КонецЦикла; 
	

КонецПроцедуры // ЗаполнитьТаблицуТекстом()

&НаКлиенте
Функция СтруктураТаблицПоЗаголовкам(ПечатнаяФорма)
	
	СтруктураТаблиц = Новый Структура;
	
	Для каждого Таблица Из ПечатнаяФорма.Tables Цикл
		
		Если ПустаяСтрока(Таблица.Title) Тогда
			Продолжить;
		КонецЕсли; 
		
		СтруктураТаблиц.Вставить(Таблица.Title,Таблица);
		
	КонецЦикла;
	
	Возврат СтруктураТаблиц;

КонецФункции // СтруктураТаблицПоЗаголовкам()

&НаКлиенте
Функция ПреобразоватьДатуВСтрокуВвода(ЗначениеДата)
	
	Если ЗначениеДата = '00010101' Тогда
		Возврат ""
	КонецЕсли; 
	
	Если Не ТипЗнч(ЗначениеДата) = Тип("Дата") Тогда
		Возврат ""
	КонецЕсли; 
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 %2 %3",
			   Формат(День(ЗначениеДата),"ЧЦ=2; ЧГ=0; ЧВН="),
			   Формат(Месяц(ЗначениеДата),"ЧЦ=2; ЧГ=0; ЧВН="),
			   Формат(Год(ЗначениеДата),"ЧЦ=4; ЧГ=0; ЧВН="));
	
КонецФункции // ПреобразоватьДату()
	
#КонецОбласти 

#Область ТестМакета

&НаКлиенте
Процедура ПрочитатьТаблицы(Команда)
	
	ОбъектWORD = Новый COMОбъект("Word.Application");
	
	ИмяВременногоФайла_Word = ПолучитьИмяВременногоФайла(".dotx");
	
	ПолучитьМакет().Записать(ИмяВременногоФайла_Word); //Двоичные данные
	
	ПечатнаяФорма = ОбъектWORD.Documents.Open(ИмяВременногоФайла_Word);
	
	ПечатнаяФорма.Activate();
	
	Для каждого Таблица Из ПечатнаяФорма.Tables Цикл
		
		Если ПустаяСтрока(Таблица.Title) Тогда
			Продолжить;
		КонецЕсли; 
		
		
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = Таблица.Title;
		СообщениеПользователю.Сообщить();
		
		
	КонецЦикла;
	
	ОбъектWORD.Quit();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьМакет()

	Возврат РеквизитФормыВЗначение("Объект").ПолучитьМакет("МакетWord")

КонецФункции // ПолучитьМакет()

&НаКлиенте
Процедура Тест(Команда)
	
	ОбъектыНазначения = Новый Массив;
	Для каждого ТекЭлементСписка Из МассивОбъектов Цикл
		ОбъектыНазначения.Добавить(ТекЭлементСписка.Значение);
	КонецЦикла; 
	Печать("ПФ_MXL_УведомлениеОРасторжении",ОбъектыНазначения);
	
КонецПроцедуры

&НаКлиенте
Процедура Справка(Команда)
	
	ОбъектыНазначения = Новый Массив;
	Для каждого ТекЭлементСписка Из МассивОбъектов Цикл
		ОбъектыНазначения.Добавить(ТекЭлементСписка.Значение);
	КонецЦикла; 
	Печать("ПФ_MXL_СправкаПриУвольнении",ОбъектыНазначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ДопЛист(Команда)
	
	ОбъектыНазначения = Новый Массив;
	Для каждого ТекЭлементСписка Из МассивОбъектов Цикл
		ОбъектыНазначения.Добавить(ТекЭлементСписка.Значение);
	КонецЦикла; 
	
	Печать("ПФ_MXL_ДополнительныйЛист",ОбъектыНазначения);

КонецПроцедуры

&НаКлиенте
Процедура Пакет(Команда)
	
	ОбъектыНазначения = Новый Массив;
	Для каждого ТекЭлементСписка Из МассивОбъектов Цикл
		ОбъектыНазначения.Добавить(ТекЭлементСписка.Значение);
	КонецЦикла; 
	
	Печать("ПФ_MXL_УведомлениеПакет",ОбъектыНазначения);

КонецПроцедуры

	
#КонецОбласти 



